// === standard headers ===
// --- HTTP client for REST requests / file download ---
#include <HTTPClient.h>

// --- TLS/SSL client for secure HTTPS connections
#include <WiFiClientSecure.h>

// --- ESP-IDF certificate bundle for TLS validation ---
#include "esp_crt_bundle.h"

// --- OTA firmware update handling ---
#include <Update.h>


// === project headers ===
// --- corresponding header ---
#include "ota.h"

// --- secrets_example.h for reference ---
#include "secrets.h"

// --- configuration ---
#include "settings.h"

// --- network ---
#include "wifi.h"

// --- services ---
#include "telegram.h"

// --- utilities ---
#include "debug.h"
#include "error.h"


/// === WIFI client setup ===
WiFiClientSecure otaClient;


/// === configure TLS with certificate bundle ===
static void initOtaClientTLS() {
    otaClient.setCACertBundle(esp_crt_bundle_attach);
}


/// === fetch the latest version of the remote firmware ===
static String fetchRemoteFirmwareVersion() {
    /// --- enable TLS certificate validation ---
    initOtaClientTLS();

    HTTPClient http;
    http.begin(otaClient, OTA_VERSION_URL);
    http.setFollowRedirects(HTTPC_STRICT_FOLLOW_REDIRECTS);

    int code = http.GET();

    DBG_PRINT("HTTP code (version): "); 
    DBG_PRINTLN(code);

    if (code != HTTP_CODE_OK) {
        error("OTA firmware version fetch failed", false);
        http.end();
        return "";
    }

    String version = http.getString();
    version.trim();

    http.end();

    return version;
}


/// === fetch update notes for the latest version of the remote firmware ===
static String fetchUpdateNotes() {
    /// --- enable TLS certificate validation ---
    initOtaClientTLS();

    HTTPClient http;
    http.begin(otaClient, OTA_UPDATE_NOTES_URL);

    http.setFollowRedirects(HTTPC_STRICT_FOLLOW_REDIRECTS);

    int code = http.GET();

    DBG_PRINT("HTTP code (notes): ");
    DBG_PRINTLN(code);

    if (code != HTTP_CODE_OK) {
        error("OTA firmware update notes fetch failed", false);
        http.end();
        return "";
    }

    String notes = http.getString();
    notes.trim();

    http.end();

    return notes;
}


/// === flash firmware OTA ===
void performFirmwareUpdateOTA(String rmtVersion) {
    /// --- get update notes ---
    String updateNotes = fetchUpdateNotes();

    /// --- enable TLS certificate validation ---
    initOtaClientTLS();

    HTTPClient http;
    http.begin(otaClient, OTA_FIRMWARE_URL);

    http.setFollowRedirects(HTTPC_STRICT_FOLLOW_REDIRECTS);

    int code = http.GET();
    if (code != HTTP_CODE_OK) {
        error("OTA firmware download failed", false);
        http.end();
    }

    int contentLength = http.getSize();
    
    if (contentLength <= 0) {
        error("OTA invalid content length", false);
        http.end();
    }
    else if (!Update.begin(contentLength)) {
        error("OTA firmware update start failed", false);
        http.end();
    }

    WiFiClient* stream = http.getStreamPtr();
    size_t written = Update.writeStream(*stream);

    if (written != contentLength) {
        error("OTA firmware incomplete write", false);
        Update.abort();
        http.end();
    }
    else if (!Update.end()) {
        error("OTA firmware update end failed", false);
        http.end();
    }
    else if (!Update.isFinished()) {
        error("OTA firmware update not finished", false);
        http.end();
    }

    /// --- notify firmware update sucess via telegram ---
    sendMsgToTelegram("Firmware updated sucessfully from " + FW_VERSION + " to " + rmtVersion);

    delay(1000);

    /// --- send firmware update notes via telegram ---
    sendMsgToTelegram("GuardianBell " + rmtVersion + ":\n" + updateNotes);

    delay(2000);
    
    http.end();

    ESP.restart();
}


/// === check for firmware update ===
void checkForFirmwareUpdate() {
    DBG_PRINTLN("Checking for firmware update");

    /// --- get remote firmware version ---
    String remoteVersion = fetchRemoteFirmwareVersion();
    if (remoteVersion.length() == 0) {
        error("No remote firmware version available", false);
        return;
    }

    DBG_PRINT("Local firmware version = ");
    DBG_PRINTLN(FW_VERSION);
    DBG_PRINT("Remote firmware version = ");
    DBG_PRINTLN(remoteVersion);

    /// --- compare local firmware version to remote firmware version ---
    if (remoteVersion == FW_VERSION) {
        DBG_PRINTLN("Firmware up to date");
        return;
    }
    else {
        DBG_PRINTLN("Firmware update available");
        DBG_PRINTLN("Attempting firmware update");
        performFirmwareUpdateOTA(remoteVersion);
    }
}
